#version 460

#define UNDEFINED -1

layout(local_size_x = 1, local_size_y = 1) in;

layout(set = 0, binding = 7, rgba32f) uniform image2D voronoiImage;

layout(set = 0, binding = 1) buffer Points {
    vec2 seedPoints[];
};

layout(push_constant) uniform Constants {
    ivec2 k;
};

void main() {
    vec2 size = vec2(imageSize(voronoiImage));

    if(k.x == 0 && k.y == 0) return;

    ivec2 id = ivec2(gl_GlobalInvocationID);
    vec4 p = imageLoad(voronoiImage, id);

    for(int j = -k.y; j <= k.y; j += k.y){
        for(int i = -k.x; i <= k.x; i += k.y ){
            if(i == 0 && j == 0) continue;

            ivec2 nid = ivec2(i, j) + id;
            if(nid.x < 0 || nid.y < 0 || nid.x >= size.x || nid.y >= size.y) {
                continue;
            }
            vec4 q = imageLoad(voronoiImage, nid);

            if(p.w == UNDEFINED && q.w != UNDEFINED) {
                p = q;
            }

            if(p.w != UNDEFINED && q.w != UNDEFINED){
                vec2 xp = vec2(id);
                vec2 sp = seedPoints[int(p.w)];
                vec2 sq = seedPoints[int(q.w)];

                if(distance(xp, sp) > distance(xp, sq)){
                    p = q;
                }
            }
        }
    }

    imageStore(voronoiImage, id, p);
}