#version 460
#include "models.glsl"
#include "cuckoo_hash_functions.glsl"

#define NUM_VERTICES 8
#define NUM_EDGES 12
#define MAX_TRIANGLE_VERTICES 16

layout(local_size_x = 32, local_size_y = 1, local_size_z = 32) in;

layout(set = 3, binding = 0, scalar) buffer EDGE_TABLE{
    uint edgeTable[256];
};
layout(set = 3, binding = 1, scalar) buffer TRI_TABLE{
    int triTable[4096];
};

layout(push_constant) uniform Constants {
    uint pass;
    uint blocksPerPass;
};

const ivec3 cube[NUM_VERTICES] = {
    ivec3(0, 0, 0),
    ivec3(1, 0, 0),
    ivec3(1, 0, 1),
    ivec3(0, 0, 1),
    ivec3(0, 1, 0),
    ivec3(1, 1, 0),
    ivec3(1, 1, 1),
    ivec3(0, 1, 1),
};

const ivec2 cubeEdges[NUM_EDGES] = {
    ivec2(0, 1), ivec2(1, 2), ivec2(2, 3), ivec2(3, 0),
    ivec2(4, 5), ivec2(5, 6), ivec2(6, 7), ivec2(7, 4),
    ivec2(0, 4), ivec2(1, 5), ivec2(2, 6), ivec2(3, 7)
};


vec3 vertexList[NUM_EDGES];
float isoValues[NUM_VERTICES];
vec3 triangles[MAX_TRIANGLE_VERTICES];
vec3 triNormals[MAX_TRIANGLE_VERTICES];

vec3 getVertex(vec3 center, int edgeIndex);
float getIsoValue(ivec3 center, int index, uint voxel_id);


shared DrawCommand drawCmd;
shared int slot;
shared int mutex;

const float cubeSize = 1.0/32.0;
const float isoLevel = 0;

uint block_index = pass * blocksPerPass + gl_WorkGroupID.y;

void take_lock();

void release_lock();

bool once();

void main() {
    vec3 box_center = block[block_index].aabb;
    const uint key = compute_hash_key(box_center);
    const uint voxel_id = block[block_index].voxel_id;
    if(block_index >= counters.blocks || skip_block(key) || counters.free_slots <= 0) {
        return;
    }

    if(gl_LocalInvocationIndex == 0) {
        drawCmd = DrawCommand(0, 1, 0, 0, vec4(box_center, block_index));
        mutex = 0;
        slot = -1;
    }
    barrier();

    const mat4 grid_to_world = camera_info.grid_to_world;
    const vec3 world_position = (grid_to_world * vec4(box_center, 1)).xyz;
    const vec3 start = world_position - vec3(0.5) + vec3(cubeSize) * 0.5;

    for(int y = 0; y < 32; ++y) {
        ivec3 location = ivec3(gl_LocalInvocationID.x, y, gl_LocalInvocationID.z);

        int config = 0;
        for(int i = 0; i < 8; ++i){
            isoValues[i] = getIsoValue(location, i, voxel_id);
            if(isoValues[i] > isoLevel){
                config |= (1 << i);
            }
        }

        if(edgeTable[config] == 0) continue;

        const vec3 position = start + vec3(location)/32.0;
        for(int edge = 0; edge < NUM_EDGES; edge++){
            if((edgeTable[config] & (1 << edge)) != 0){
                vertexList[edge] = getVertex(position, edge);
            }
        }

        uint numVertices = 0;
        for(int i = config * MAX_TRIANGLE_VERTICES; triTable[i] != -1; i+= 3){
            vec3 p0 = vertexList[triTable[i]];
            vec3 p1 = vertexList[triTable[i + 1]];
            vec3 p2 = vertexList[triTable[i + 2]];

            triangles[numVertices++] = p0;
            triangles[numVertices++] = p1;
            triangles[numVertices++] = p2;
        }
        uint vOffset = atomicAdd(drawCmd.vertexCount, numVertices);

        if(once()) {
            int acquire_slot = atomicAdd(counters.free_slots, -1);
            if(acquire_slot > 0) {
                slot = atomicAdd(counters.slots_used, 1);
            }
        }
        barrier();
        if(slot == -1) break;

        for(uint i = 0; i < numVertices; ++i) {
            uint vId = i + vOffset;
            vertex[slot].data[vId].position = triangles[i];
        }
    }

    barrier();
    if(slot != -1 && gl_LocalInvocationIndex == 0) {
        if(drawCmd.vertexCount > 0) {
            draw[slot] = drawCmd;
            block[block_index].vertex_id = pass;

            const uint processed_block_add_id = atomicAdd(counters.processed_block_add_id, 1);
            processed_block_keys[processed_block_add_id] = key;
        }else {
            const uint empty_block_add_id = atomicAdd(counters.empty_block_add_id, 1);
            empty_block_keys[empty_block_add_id] = key;
        }

    }
}



float getIsoValue(ivec3 center, int index, uint voxel_id){
    ivec3 location = center + cube[index];
    return imageLoad(voxels[voxel_id],  location).r;
}

vec3 getVertex(vec3 center, int edgeIndex) {
    ivec2 edge = cubeEdges[edgeIndex];
    vec3 p0 = center + cube[edge[0]] * cubeSize;
    vec3 p1 = center + cube[edge[1]] * cubeSize;

    float isoVal0 = isoValues[edge[0]];
    float isoVal1 = isoValues[edge[1]];
    float t = (isoLevel - isoVal0)/(isoVal1 - isoVal0);

    return mix(p0, p1, t);
}


bool once() {
    return atomicCompSwap(mutex, 0, 1) == 0;
}

void take_lock() {
    do { } while (atomicCompSwap(mutex, 0, 1) == 0);
}

void release_lock() {
    mutex = 0;
}